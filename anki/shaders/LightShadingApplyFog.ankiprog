// Copyright (C) 2009-2020, Panagiotis Christopoulos Charitos and contributors.
// All rights reserved.
// Code licensed under the BSD License.
// http://www.anki3d.org/LICENSE

ANKI_SPECIALIZATION_CONSTANT_U32(FOG_LAST_CLASTER, 0, 1u);

#pragma anki start vert
#include <anki/shaders/QuadVert.glsl>
#pragma anki end

#pragma anki start frag

#include <anki/shaders/include/ClusteredShadingTypes.h>
#include <anki/shaders/include/ClusteredShadingFunctions.h>
#include <anki/shaders/Common.glsl>

layout(location = 0) in Vec2 in_uv;
layout(location = 0) out Vec4 out_color;

layout(set = 0, binding = 0) uniform sampler u_nearestAnyClampSampler;
layout(set = 0, binding = 1) uniform sampler u_linearAnyClampSampler;
layout(set = 0, binding = 2) uniform texture2D u_depthRt;
layout(set = 0, binding = 3) uniform texture3D u_fogVolume;

layout(push_constant, std140, row_major) uniform pc_
{
	ClustererMagicValues u_clustererMagic;
	Mat4 u_invViewProjMat;
};

void main()
{
	const F32 depth = textureLod(u_depthRt, u_nearestAnyClampSampler, in_uv, 0.0).r;

	// Get world position
	const Vec4 worldPos4 = u_invViewProjMat * Vec4(UV_TO_NDC(in_uv), depth, 1.0);
	const Vec3 worldPos = worldPos4.xyz / worldPos4.w;

	// Read the volume
	const Vec3 uv3d = computeClustererVolumeTextureUvs(u_clustererMagic, in_uv, worldPos, FOG_LAST_CLASTER + 1u);
	const Vec4 fogVals = textureLod(u_fogVolume, u_linearAnyClampSampler, uv3d, 0.0);
	const Vec3 inScattering = fogVals.rgb;
	const F32 transmittance = fogVals.a;

	// Apply the fog
	out_color = Vec4(inScattering, transmittance);
}

#pragma anki end
